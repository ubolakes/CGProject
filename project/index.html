<!--
    Author: Umberto Laghi
    E-mail: umberto.laghi@studio.unibo.it
    Github: @ubolakes
-->

<!DOCTYPE html>
<html>
    <head>
        <title>CG Project</title>

        <meta http-equiv="Pragma" content="no-cache">
        <meta http-equiv="Cache-Control" content="no-cache">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">

        <!-- CSS -->
        <link rel="stylesheet" type="text/css" href="style/style.css">

        <!-- base shader program -->
        <!-- used to render meshes w/out shadows -->
        <script id="base-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 a_position;
            attribute vec3 a_normal;
            attribute vec2 a_texcoord;
            attribute vec4 a_color;

            uniform mat4 u_projection;          // projection matrix
            uniform mat4 u_view;                // camera view matrix
            uniform mat4 u_world;               // world matrix
            uniform vec3 u_viewWorldPosition;   // camera position

            varying vec3 v_normal;
            varying vec3 v_surfaceToView;
            varying vec2 v_texcoord;
            varying vec4 v_color;

            void main() {
                vec4 worldPosition = u_world * a_position;
                gl_Position = u_projection * u_view * worldPosition;

                // passing data to fragment shader
                v_surfaceToView = u_viewWorldPosition - worldPosition.xyz;
                v_normal = mat3(u_world) * a_normal;
                v_texcoord = a_texcoord;
                v_color = a_color;
            }
        </script>

        <script id="base-fragment-shader" type="x-shader/x-fragment">
            precision highp float;

            varying vec3 v_normal;
            varying vec2 v_texcoord;
            varying vec4 v_color;
            varying vec3 v_surfaceToView;

            // material properties
            uniform sampler2D diffuseMap;
            uniform sampler2D normalMap;
            uniform vec3 diffuse;
            uniform vec3 ambient;
            uniform vec3 emissive;
            uniform vec3 specular;
            uniform float shininess;
            uniform float opacity;
            // light properties
            uniform vec3 u_lightDirection;
            uniform vec3 u_lightColor;
            uniform vec3 u_ambientLight;

            void main() {
                vec3 normal = normalize(v_normal) * (float(gl_FrontFacing) * 2.0 -1.0);

                vec3 surfaceToViewDirection = normalize(v_surfaceToView);
                vec3 halfVector = normalize(u_lightDirection + surfaceToViewDirection);

                float fakeLight = dot(u_lightDirection, normal) * 0.5 + 0.5;
                float specularLight = clamp(dot(normal, halfVector), 0.0, 1.0);

                vec4 diffuseMapColor = texture2D(diffuseMap, v_texcoord);
                vec3 effectiveDiffuse = diffuse * diffuseMapColor.rgb * u_lightColor.rgb * v_color.rgb;
                float effectiveOpacity = opacity * diffuseMapColor.a * v_color.a;

                gl_FragColor = vec4(
                    emissive +
                    ambient * u_ambientLight +
                    effectiveDiffuse * fakeLight +
                    specular * pow(specularLight, shininess),
                    effectiveOpacity
                );
            }
        </script>

        <!-- skybox shader program -->
        <!-- used to render the skybox as a cubemap -->
        <script id="skybox-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 a_position;
            varying vec4 v_position;

            void main() {
                gl_Position = vec4(a_position.xy, 1, 1);
                v_position = a_position;
            }
        </script>

        <script id="skybox-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;

            uniform samplerCube u_skybox;
            uniform mat4 u_viewDirectionProjectionInverse;
            uniform vec3 u_lightColor;

            varying vec4 v_position;

            void main() {
                vec4 t = u_viewDirectionProjectionInverse * v_position;
                gl_FragColor = textureCube(u_skybox, normalize(t.xyz / t.w)) * vec4(u_lightColor, 1);
            }
        </script>

        <!-- shadow shader program -->
        <!-- used to render shadows -->
        <script id="shadow-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 a_position;
            attribute vec2 a_texcoord;
            attribute vec3 a_normal;
            attribute vec4 a_color;

            uniform mat4 u_projection;
            uniform mat4 u_view;
            uniform mat4 u_world;
            uniform mat4 u_textureMatrix;

            varying vec2 v_texcoord;
            varying vec4 v_projectedTexcoord;
            varying vec3 v_normal;
            varying vec4 v_color;

            void main() {
                vec4 worldPosition = u_world * a_position;
                gl_Position = u_projection * u_view * worldPosition;

                // passing data to fragment shader
                v_texcoord = a_texcoord;
                v_projectedTexcoord = u_textureMatrix * worldPosition;
                v_normal = mat3(u_world) * a_normal;
                v_color = a_color;
            }
        </script>

        <script id="shadow-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;

            varying vec2 v_texcoord;
            varying vec4 v_projectedTexcoord;
            varying vec3 v_normal;
            varying vec4 v_color;

            // shadow properties
            uniform vec4 u_colorMult;
            uniform sampler2D u_texture;
            uniform sampler2D u_projectedTexture;
            uniform float u_bias;
            uniform vec3 u_reverseLightDirection;
            // material properties
            uniform sampler2D diffuseMap;
            uniform sampler2D normalMap;
            uniform vec3 diffuse;
            uniform vec3 ambient;
            uniform vec3 emissive;
            uniform vec3 specular;
            uniform float shininess;
            uniform float opacity;

            void main() {
                vec3 normal = normalize(v_normal);

                float light = dot(normal, u_reverseLightDirection);
                
                vec3 projectedTexcoord = v_projectedTexcoord.xyz / v_projectedTexcoord.w;
                float currentDepth = projectedTexcoord.z + u_bias;

                bool inRange = 
                    projectedTexcoord.x >= 0.0 &&
                    projectedTexcoord.x <= 1.0 &&
                    projectedTexcoord.y >= 0.0 &&
                    projectedTexcoord.y <= 1.0;
                
                float projectedDepth = texture2D(u_projectedTexture, projectedTexcoord.xy).r;
                float shadowLight = (inRange && projectedDepth <= currentDepth) ? 0.0 : 1.0;

                vec4 diffuseMapColor = texture2D(diffuseMap, v_texcoord);
                vec3 effectiveDiffuse = diffuse * diffuseMapColor.rgb * v_color.rgb;
                float effectiveOpacity = opacity * diffuseMapColor.a * v_color.a;

                vec4 texColor = texture2D(u_texture, v_texcoord) * u_colorMult;

                gl_FragColor = vec4(
                    effectiveDiffuse.rgb * light * shadowLight,
                    effectiveOpacity
                );
            }
        </script>

        <!-- color shader program -->
        <!-- used to render flat color -->
        <script id="color-vertex-shader" type="x-shader/x-vertex">
            attribute vec4 a_position;

            uniform mat4 u_projection;
            uniform mat4 u_view;
            uniform mat4 u_world;

            void main() {
                gl_Position = u_projection * u_view * u_world * a_position;
            }
        </script>

        <script id="color-fragment-shader" type="x-shader/x-fragment">
            precision mediump float;

            uniform vec4 u_color;

            void main() {
                gl_FragColor = u_color;
            }
        </script>


        <!-- including JS source files -->
        <!-- libraries-->
        <script src="resources/webgl-utils.js"      defer></script>
        <script src="resources/m4.js"               defer></script>
        <script src="resources/dat.gui.js"          defer></script>
        <script src="resources/glm_utils.js"        defer></script>
        <script src="resources/jquery-3.6.0.js"     defer></script>
        <script src="resources/mesh_utils.js"       defer></script>
        <script src="resources/load_mesh.js"        defer></script>
        <script src="resources/dat.gui_manager.js"  defer></script>
        <script src="resources/utils.js"            defer></script>
        <!-- scene -->
        <script src="scene/camera.js"               defer></script>
        <script src="scene/interaction.js"          defer></script>
        <script src="scene/mesh_obj.js"             defer></script>
        <script src="scene/scene.js"                defer></script>
        <!-- main -->
        <script src="main.js" defer></script>
    </head>

    <body>
        <div class="row">

			<div id="main" class="column1">
				<canvas id="canvas" height="1000px" width="1000px"></canvas>
			</div>

			<div class="menu column2">
				<div class = "menu-row">
					<details>
						<summary class="title">
							Camera
						</summary>

                        <button class="btn" onmouseup="scene.camera.align();">
                            Align
						</button>

						<details>
							<summary class="title">
								Keyboard
							</summary>
							<p style="text-align: left">
								<b>W/S</b> : move forward/backward <br>
								<b>A/D</b> : move left/right <br>
								<b>Spacebar/Left Ctrl</b> : move up/down <br>
								<b>Arrow UP/DOWN</b> : tilt movement<br>
								<b>Arrow LEFT/RIGHT</b> : pan movement <br>
								<b>R : realign camera</b>
							</p>
						</details>
					</details>
				</div>
			</div>

			<div class="menu column2">
				<details>
					<summary class="title">
						Settings
					</summary>
					<div id ="gui">
					</div>

				</details>

			</div>






		</div>

    </body>
</html>